plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'maven-publish'
}

android {
    namespace = "hmju.http.tracking"
    compileSdk 34
    defaultConfig {
        minSdk 23
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
}

dependencies {
    api(project(":interceptor"))

    /**
     * Android X
     */
    implementation("androidx.appcompat:appcompat:1.3.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.2.0")
    implementation("com.google.android.material:material:1.3.0")
    implementation("androidx.constraintlayout:constraintlayout:2.0.4")

    /**
     * Coroutines
     */
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2")

    /**
     * Network
     */
    implementation("com.squareup.okhttp3:okhttp:4.9.3")

    /**
     * Gson Builder
     */
    implementation("com.google.code.gson:gson:2.8.9")
}

afterEvaluate {
    publishing {
        publications {
            deploy(MavenPublication) {
                from components.release
                groupId = 'com.github.sieunju.httptracking'
                artifactId = 'ui'
                version = deps.release

                pom {
                    name = 'HTTP Tracking Library'
                    description = 'HTTP tracking detector library'
                    url = 'https://github.com/sieunju/httptracking'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'sieunju'
                            name = 'jimbo'
                            email = 'j.sieun73@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/sieunju/httptracking.git'
                        developerConnection = 'scm:git:ssh://git@github.com:sieunju/httptracking.git'
                        url = 'https://github.com/sieunju/httptracking'
                    }
                }
            }
        }
    }
}